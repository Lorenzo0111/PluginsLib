plugins {
    id 'java'
    id 'maven-publish'
    id "com.github.johnrengelman.shadow" version "7.0.0"
}

version "2.0"
description "Library for minecraft plugins"

ext {
    base = 'me.lorenzo0111.pluginslib.libraries'
}


shadowJar {
    archiveFileName = "${project.name}-${project.version}.jar"
}

subprojects {
    apply plugin: "java"
    apply plugin: "maven-publish"
    apply plugin: "com.github.johnrengelman.shadow"

    group 'space.rocketplugins.pluginslib'
    version '2.0.1.2'

    repositories {
        mavenCentral()
    }

    javadoc {
        options.links(
                'https://javadoc.io/static/org.jetbrains/annotations/20.1.0/',
                'https://docs.oracle.com/javase/8/docs/api/',
                'https://papermc.io/javadocs/paper/1.16/'
        )
        source = sourceSets.main.allJava
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    repositories {
        maven { url = uri("https://repo.vshnv.tech/releases") }
    }

    dependencies {
        compileOnly 'org.jetbrains:annotations:21.0.1'
        implementation 'io.github.slimjar:slimjar:1.2.4'
    }

    shadowJar {
        archiveFileName = "${project.name}-${project.version}.jar"
    }

    shadowJar {
        relocate("org.yaml.snakeyaml","${base}.yaml")
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java

                pom {
                    name = 'PluginsLib'
                    description = 'A library for minecraft plugins'
                    url = 'https://github.com/Lorenzo0111/PluginsLib'

                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://github.com/Lorenzo0111/PluginsLib/blob/master/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'lorenzo0111'
                            name = 'Lorenzo0111'
                            email = 'lorenzo@rocketplugins.space'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/Lorenzo0111/PluginsLib.git'
                        developerConnection = 'scm:git:ssh://github.com/Lorenzo0111/PluginsLib.git'
                        url = 'https://github.com/Lorenzo0111/PluginsLib'
                    }
                }

            }
        }

        repositories {
            maven {
                url 'https://repo.repsy.io/mvn/lorenzo0111/public'
                credentials {
                    username System.getenv("REPO_USERNAME")
                    password System.getenv("REPO_PASSWORD")
                }
            }
        }

        publications {
            gpr(MavenPublication) {
                from(components.java)
            }
        }
    }

    tasks.build.dependsOn tasks.shadowJar
}