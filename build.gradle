plugins {
    id 'java'
    id 'maven-publish'
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

version "2.0.4"
description "Library for minecraft plugins"

ext {
    base = 'me.lorenzo0111.pluginslib.libraries'
}

shadowJar {
    archiveFileName = "${project.name}-${project.version}.jar"
}

subprojects {
    apply plugin: "java"
    apply plugin: "maven-publish"
    apply plugin: "com.github.johnrengelman.shadow"

    group 'me.lorenzo0111.pluginslib'
    version '2.0.4'

    repositories {
        mavenCentral()
    }

    javadoc {
        options.links(
                'https://javadoc.io/static/org.jetbrains/annotations/24.1.0/',
                'https://docs.oracle.com/javase/8/docs/api/',
                'https://jd.papermc.io/paper/1.20.4/'
        )
        source = sourceSets.main.allJava
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    repositories {
        maven { url = uri("https://dl.lorenzo0111.me/releases") }
    }

    dependencies {
        compileOnly 'org.jetbrains:annotations:24.1.0'
        implementation 'io.github.slimjar:slimjar:1.2.8'
    }

    shadowJar {
        archiveFileName = "${project.name}-${project.version}.jar"
        relocate("org.yaml.snakeyaml", "${base}.yaml")
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java

                pom {
                    name = 'PluginsLib'
                    description = 'A library for minecraft plugins'
                    url = 'https://github.com/Lorenzo0111/PluginsLib'

                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://github.com/Lorenzo0111/PluginsLib/blob/master/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'lorenzo0111'
                            name = 'Lorenzo0111'
                            email = 'hello@lorenzo0111.me'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/Lorenzo0111/PluginsLib.git'
                        developerConnection = 'scm:git:ssh://github.com/Lorenzo0111/PluginsLib.git'
                        url = 'https://github.com/Lorenzo0111/PluginsLib'
                    }
                }

            }
        }

        repositories {
            maven {
                url 'https://dl.lorenzo0111.me/releases'
                credentials {
                    username = project.findProperty("repoUsername") ?: repoUsername
                    password = project.findProperty("repoPassword") ?: repoPassword
                }
            }
        }
    }

    tasks.build.dependsOn tasks.shadowJar
}
